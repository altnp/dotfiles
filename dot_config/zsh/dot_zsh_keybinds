#!/bin/zsh
bindkey -e

bindkey '^[[Z' reverse-menu-complete
bindkey '^[[H' beginning-of-line
bindkey '^[[F' end-of-line

r-delregion() {
  if ((REGION_ACTIVE)) then
     zle kill-region
  else
    local widget_name=$1
    shift
    zle $widget_name -- $@
  fi
}

r-deselect() {
  ((REGION_ACTIVE = 0))
  local widget_name=$1
  shift
  zle $widget_name -- $@
}

r-select() {
  ((REGION_ACTIVE)) || zle set-mark-command
  local widget_name=$1
  shift
  zle $widget_name -- $@
}

for key     kcap   seq        mode   widget (
    sleft   kLFT   $'\e[1;2D' select   backward-char
    sright  kRIT   $'\e[1;2C' select   forward-char
    sup     kri    $'\e[1;2A' select   up-line-or-history
    sdown   kind   $'\e[1;2B' select   down-line-or-history

    send    kEND   $'\E[1;2F' select   end-of-line
    send2   x      $'\E[4;2~' select   end-of-line

    shome   kHOM   $'\E[1;2H' select   beginning-of-line
    shome2  x      $'\E[1;2~' select   beginning-of-line

    left    kcub1  $'\EOD'    deselect backward-char
    right   kcuf1  $'\EOC'    deselect forward-char

    end     kend   $'\EOF'    deselect end-of-line
    end2    x      $'\E4~'    deselect end-of-line

    home    khome  $'\EOH'    deselect beginning-of-line
    home2   x      $'\E1~'    deselect beginning-of-line

    csleft  x      $'\E[1;6D' select   backward-word
    csright x      $'\E[1;6C' select   forward-word
    csend   x      $'\E[1;6F' select   end-of-line
    cshome  x      $'\E[1;6H' select   beginning-of-line

    cleft   x      $'\E[1;5D' deselect backward-word
    cright  x      $'\E[1;5C' deselect forward-word

    del     kdch1   $'\E[3~'  delregion delete-char
    bs      x       $'^?'     delregion backward-delete-char

  ) {
  eval "key-$key() {
    r-$mode $widget \$@
  }"
  zle -N key-$key
  bindkey ${terminfo[$kcap]-$seq} key-$key
}

# restore backward-delete-char for Backspace in the incremental
# search keymap so it keeps working there:
bindkey -M isearch '^?' backward-delete-char

# Custom Widget for Ctrl+Home
delete-until-beginning-of-line() {
  local BUFFER_backup="$BUFFER"
  local CURSOR_backup="$CURSOR"
  BUFFER=${BUFFER:$CURSOR}
  CURSOR=0
  zle -f kill # Add deleted part to kill ring
}
zle -N delete-until-beginning-of-line
bindkey '^[[1;5H' delete-until-beginning-of-line # bind Ctrl+Home

# Custom Widget for Ctrl+End
delete-until-end-of-line() {
  BUFFER=${BUFFER:0:$CURSOR}
  zle -f kill # Add deleted part to kill ring
}
zle -N delete-until-end-of-line
bindkey '^[[1;5F' delete-until-end-of-line # bind Ctrl+End

bindkey '^H' backward-kill-word
bindkey '5~' kill-word

select_all() {
  CURSOR=0
  zle set-mark-command
  CURSOR=${#BUFFER}
}
zle -N select_all
bindkey '^A' select_all

function zle_paste_clipboard {
  LBUFFER+=$(xclip -o -selection clipboard)
}
zle -N zle_paste_clipboard
bindkey '^V' zle_paste_clipboard
